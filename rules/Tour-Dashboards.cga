/**
 * Author:  Esri R&D Center Zurich & Devin Lavigne, Houseal Lavigne Associates
 */

version "2024.0"


###   CONSTANTS

const unitScale = case Units == "Meters" : 1 else: 0.304800		# If Meters the scale is 1, otherwise we convert to feet
const areaScale = case Units == "Meters" : 1 else: 10.7639		# If Meters the scale is 1, otherwise we convert square meters to square feet
const Floor_Height = 3											# Floor heights are 3 meters (approximately 10 feet)

###   ATTRIBUTES

@Group("Global",1) @Order(1)
@Description("The units for the selected shapes")
@Enum("Feet","Meters")
attr Units = "Meters"

@Description("An assumption for the average size of a dwelling unit")
@Range(min=50,max=2500,stepsize=5)
attr DwellingUnitSize  = case Units == "Meters" : 100 else: 1000

@Description("An assumption employment density, default is US Average, 1 per 200sf (approx 20m2)")
@Range(min=0,max=1000,stepsize=50)
attr SquareFeetPerEmployee = 150


@Group("Building Attributes",2)
@Description("The height of the building in the selected units")
@Order(1)
@Handle(shape=SplitFloors, align=left)
@Range(min=0,max=200,stepsize=1)
attr BuildingHeight = 30 / unitScale

@Order(2)
@Description("The land use designation for the building")
@Enum("Multi-Family", "Office", "Commercial", "Mixed-Use", "Hotel", "Public", "Park", "Parking", "None")
attr LandUse = "None"

@Description("The depth of the building")
@Order(3)
@Range(min=1,max=200,stepsize=1)
attr BuildingDepth = 30 /unitScale



@Group("Land Use Colors",10)
@Color @Order(1)
attr MultiFamily_Color	= "#A97C50"

@Color @Order(2)
attr Office_Color		= "#6699FF"

@Color @Order(3)
attr Commercial_Color	= "#FF6633"

@Color @Order(4)
attr MixedUse_Color		= "#77393A"

@Color @Order(5)
attr Hotel_Color		= "#FFCC33"

@Color @Order(7)  
attr Public_Color		= "#66FFFF"

@Color @Order(8)
attr Park_Color			= "#709B4F"

@Color @Order(9)
attr Parking_Color		= "#9E9E9E"


###   FUNCTIONS

_getColor = 
	case LandUse=="Multi-Family" 	: MultiFamily_Color
	case LandUse=="Office" 			: Office_Color
	case LandUse=="Commercial" 		: Commercial_Color	
	case LandUse=="Mixed-Use" 		: MixedUse_Color
	case LandUse=="Hotel" 			: Hotel_Color
	case LandUse=="Public" 			: Public_Color
	case LandUse=="Park" 			: Park_Color
	case LandUse=="Parking"			: Parking_Color
	else							: "#FFFFFF" 		# White

_getBldgDepth = BuildingDepth * unitScale

### START RULE

@StartRule
Generate-->
	color(_getColor)
	CheckPark
	
CheckPark-->
	case LandUse == "Park" 	: BuildPark
	else 					: CheckParking

CheckParking-->
	case LandUse == "Parking" 	: ParkingLot
	else						: Building

BuildPark-->
	set(LandUse,"Park")		# allows remainder areas of Buildings to calculate area
	color(Park_Color)
	ReportArea
	Parkland.

ParkingLot-->
	color(Parking_Color)
	ReportArea
	Parking.
		
Building-->
	extrude(BuildingHeight * unitScale)
	SplitFloors
	
SplitFloors-->
	split(y){~Floor_Height:FloorPlate | 0.75:NIL}*

FloorPlate-->
	comp(f) { top		: Top.
			| bottom	: ReportArea Floor. 
			| side		: Wall.}

ReportArea-->
	report("Area (m2)",geometry.area)
	report("Area (m2)" + "." + LandUse,geometry.area)
	report("Area (m2)" + "." + LandUse + "#color",_getColor)
	report("Area (sf)",geometry.area * areaScale)
	report("Area (sf)" + "." + LandUse,geometry.area * areaScale)
	report("Area (sf)" + "." + LandUse + "#color",_getColor)
	ReportSpecifics

ReportSpecifics-->
	case LandUse == "Multi-Family"		: report("Dwelling Units", geometry.area*areaScale/DwellingUnitSize)
	case LandUse == "Office"			: report("Employees",geometry.area*unitScale/SquareFeetPerEmployee) 
	case LandUse == "Mixed-Use"			: report("Employees",geometry.area/2*unitScale/SquareFeetPerEmployee)   	#For demo purposes only we are dividing a mixed use building in half with half										  
										  report("Dwelling Units", geometry.area/2*areaScale/DwellingUnitSize)		#of area counting dwelling units and the other half counting employees
	else: NIL	

